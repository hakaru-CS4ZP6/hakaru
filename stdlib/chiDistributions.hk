# Hakaru Standard Library
# author: Mahmoud Khattab 

##############################
# (Cental) Chi Distributions #
##############################

# The Chi Squared distribution is defined in terms of N independant normal distributions

# This function takes 2 arrays of size N, containing the means and standard deviations
# of the N normal distributions.
def chiSq(means array(real), stdevs array(prob)) measure(prob):
	#TODO: error check sizes of means==stdevs
	q <~ plate _ of size(means): normal(means[_],stdevs[_])
	return real2prob(summate i from 0 to size(q): ((q[i]-means[i])/stdevs[i])^2)

# The chi distribution. Takes 2 arrays of size N like above.
def chi(means array(real), stdevs array(prob)) measure(prob):
	q <~ chiSq(means,stdevs)
	return sqrt(q)

# Chi Squared distribution with n independant and identical distributions (iid).
def chiSq_iid(n nat, mean real, stdev prob) measure(prob):
	q <~ plate _ of n: normal(mean,stdev)
	return real2prob(summate i from 0 to size(q): ((q[i]-mean)/stdev)^2)

# Chi distribution with n independant and identical distributions (iid).
def chi_iid(n nat, mean real, stdev prob) measure(prob):
	q <~ chiSq_iid(n,mean,stdev)
	return sqrt(q)

# Standard Chi Squared distribution is defined in terms of n independant
# standard normal distributions
def stdChiSq(n nat) measure(prob):
	chiSq_iid(n,0,1)

def stdChi(n nat) measure(prob):
	q <~ stdChiSq(n)
	return sqrt(real2prob(q))

####################################
# Chi distribution Transformations #
####################################

# F distribution "arises frequently as the null distribution of a test statistic, most notably in the analysis of variance, e.g., F-test." - Wikipedia
def F(n1 nat, n2 nat) measure(prob):
	X1 <~ stdChiSq(n1)
	X2 <~ stdChiSq(n2)
	return (X1*n2)/(X2*n1)

# The Non Central T Distribution defined in terms of
#  - Number of samples: n
#  - Non centrality parameter: delta
def nonCentralT(n nat, delta real) measure(real):
	U <~ normal(0,1)
	X <~ stdChi(n)
	return (U + delta)*sqrt(n)/(X)

# T distribution approximates normal distribution when 
# sample size is small and standard deviation is unknown
def t(n nat) measure(real):
	nonCentralT(n,0)
